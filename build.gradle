plugins {
    id 'java'
    id 'org.springframework.boot' version '2.7.5'
    id 'io.spring.dependency-management' version '1.0.15.RELEASE'
    id 'com.diffplug.spotless' version '6.11.0'
}

group = 'de.unibamberg.dsam.group6'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '17'

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
    developmentOnly
    runtimeClasspath {
        extendsFrom developmentOnly
    }
}

repositories {
    mavenCentral()
}

dependencies {
    //region Spring Boot
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-jdbc'
    implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-security'

    developmentOnly 'org.springframework.boot:spring-boot-devtools'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    //endregion

    //region Spring other
    implementation 'org.springframework.session:spring-session-jdbc'
    implementation 'org.springframework.security:spring-security-test'
    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
    //endregion

    //region thymeleaf
    implementation 'org.thymeleaf.extras:thymeleaf-extras-springsecurity5'
    implementation 'com.github.bufferings:thymeleaf-extras-nl2br:1.0.2'
    implementation 'org.thymeleaf.extras:thymeleaf-extras-java8time:3.0.4.RELEASE'
    //endregion

    implementation 'org.hibernate.validator:hibernate-validator'
    implementation 'org.postgresql:postgresql'
    implementation 'org.apache.commons:commons-lang3'
    implementation 'com.fasterxml.jackson.core:jackson-core'
    implementation 'com.google.code.gson:gson'
    developmentOnly 'com.h2database:h2'

    annotationProcessor 'org.projectlombok:lombok'
    compileOnly 'org.projectlombok:lombok'
}

bootJar {
    archiveName "prost.jar"
}

tasks.named('test') {
    useJUnitPlatform()
}

spotless {
    format 'misc', {
        target '*.gradle', '*.md', '.gitignore'

        trimTrailingWhitespace()
        indentWithSpaces()
        endWithNewline()
    }

    java {
        target 'src/*/java/**/*.java', 'invoicing-function/java/**/*.java'
        indentWithSpaces(4)
        palantirJavaFormat()
    }
}
